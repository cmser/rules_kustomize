load("//kustomize:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")

def _kustomize_repositories_impl(repository_ctx):
    repository_ctx.download_and_extract(
        url = repository_ctx.attr.url,
    )
    build_content = """#Generated by kustomize/repositories.bzl
load("@com_github_cmser_rules_kustomize//:toolchain.bzl", "kustomize_toolchain")
kustomize_toolchain(name = "kustomize_toolchain", bin = "kustomize")
"""

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

kustomize_repositories = repository_rule(
    implementation = _kustomize_repositories_impl,
    attrs = {
        "url": attr.string(mandatory = True),
        "platform": attr.string(mandatory = True),
    }
)

def kustomize_register_toolchains(name = "kustomize", version = "v4.4.1", register = True, **kwargs):
    for platform, platform_value in PLATFORMS.items():
        kustomize_repositories(
            name = name + "_" + platform,
            platform = platform,
            url = platform_value.url.format(version, version),
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )